#pragma kernel InBlockRadix
#pragma kernel GlobalScatter

#include "./RadixHelper.hlsl"

// InputItems is particle number/id/PIndex	
// InputKeys is cell key 

// [1024 ^ 2] or [GroupNum * GroupSize]
RWStructuredBuffer<uint> InputIndex;	
RWStructuredBuffer<uint> InputKeys;
RWStructuredBuffer<uint> SortedIndex;
RWStructuredBuffer<uint> SortedKeys;

// [1024 * 16] or [GroupNum * BucketNum]   
RWStructuredBuffer<uint> BucketCounter;
RWStructuredBuffer<uint> DstCounter;

// [1024 ^ 2] or [GroupNum * GroupSize]
RWStructuredBuffer<uint> GlobalPSum;    

groupshared uint localPrefix[THREAD_NUM_X];

uint numInputs;
uint currIteration;
uint g_BlocksNums;
uint g_CounterNums;         
// g_BlocksNums = 1024, g_CounterNums= 16 * 1024


void PrefixSumLocal(uint IGid : SV_GroupIndex, uint IScanIndex)
{
    uint d = 0;
    uint i = 0;
    uint offset = 1;
    uint totalNum = THREAD_NUM_X;
    
    // Up sweep
    [unroll]
    for (d = totalNum>>1; d > 0; d >>= 1)
    {
        GroupMemoryBarrierWithGroupSync();
        if (IGid < d)
        {
            uint indexA = offset * (2 * IGid + 1) - 1;
            uint indexB = offset * (2 * IGid + 2) - 1;

            localPrefix[indexB] += localPrefix[indexA];
        }
        offset *= 2;
    }

    if (IGid == 0)
    {
        BucketCounter[IScanIndex] = localPrefix[totalNum - 1];
        localPrefix[totalNum - 1] = 0;
    }
    
    GroupMemoryBarrierWithGroupSync();

    // Down sweep
    [unroll]
    for (d = 1; d < totalNum; d *= 2)  
    {
        offset >>= 1;
        GroupMemoryBarrierWithGroupSync();

        if (IGid < d)
        {
            uint indexA = offset * (2 * IGid + 1) - 1;
            uint indexB = offset * (2 * IGid + 2) - 1;

            uint sum = localPrefix[indexA];
            localPrefix[indexA] = localPrefix[indexB];
            localPrefix[indexB] += sum;
        }
    }
    
    GroupMemoryBarrierWithGroupSync();
}

[numthreads(THREAD_NUM_X, 1, 1)]
void InBlockRadix(uint GI : SV_GroupIndex, uint3 DTid : SV_DispatchThreadID, uint3 Gid : SV_GroupID)
{
    uint digit = get4Bits(InputKeys[DTid.x], currIteration);
    
    [unroll(RADIX_R)]
    for (uint r = 0; r < RADIX_R; ++r)
    {
        localPrefix[GI] = (digit == r ? 1 : 0);
        GroupMemoryBarrierWithGroupSync();
        PrefixSumLocal(GI, r * g_BlocksNums + Gid.x);
        // now localPrefix[] is EScan of current block.
        
        if(digit == r)  GlobalPSum[DTid.x] = localPrefix[GI];
        GroupMemoryBarrierWithGroupSync();
    }
}

[numthreads(THREAD_NUM_X, 1, 1)]
void GlobalScatter(uint3 DTid : SV_DispatchThreadID,uint3 Gid:SV_GroupID)
{
    uint digit = get4Bits(InputKeys[DTid.x], currIteration);

    uint counterIndex = digit * g_BlocksNums + Gid.x;
    uint globalOffset = GlobalPSum[DTid.x] + DstCounter[counterIndex];
    SortedKeys[globalOffset] = InputKeys[DTid.x];
    SortedIndex[globalOffset] = InputIndex[DTid.x];
}