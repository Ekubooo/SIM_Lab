#pragma kernel UpdateSpatialHash
#pragma kernel Reorder
#pragma kernel ReorderCopyBack 

#pragma kernel ApplyAndPredict
#pragma kernel CalcLagrangeOperator
#pragma kernel CalcDeltaPosition
#pragma kernel UpdatePredictPosition
#pragma kernel UpdateProperty           
#pragma kernel VorticityAndViscosity    

#pragma kernel UpdateDensityTexture
#pragma kernel UpdateWhiteParticles
#pragma kernel WhiteParticlePrepareNextFrame;


#include "./PBF3D.hlsl"

static const int ThreadGroupSize = 256;

// todo: Refactor the compute shaer file and updateSettings func in main script.
// todo: Rename the PBF3D.hlsl and pass the constant value in update setting.       // finish and check again
// todo: make sure the radius check of neighbor search is correct.                  // check
// todo: buffer for kernel not bind yet.        
// todo: buffer which new set is not init yet. 
// todo: interaction 3d, gravity point and fluid planet

[numthreads(ThreadGroupSize, 1, 1)]
void UpdateSpatialHash(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    uint index = id.x;
    int3 cell = GetCell3D(PredictedPositions[index], smoothingRadius);
    uint hash = HashCell3D(cell);
    uint key = KeyFromHash(hash, numParticles);
    
    SortedIndices[id.x] = id.x;
    SpatialKeys[id.x] = key;
}

[numthreads(ThreadGroupSize, 1, 1)]
void Reorder(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;
    uint sortedIndex = SortedIndices[id.x];
    SortTarget_Positions[id.x] = Positions[sortedIndex];
    SortTarget_PredictedPositions[id.x] = PredictedPositions[sortedIndex];
    SortTarget_Velocities[id.x] = Velocities[sortedIndex];
}

[numthreads(ThreadGroupSize, 1, 1)]
void ReorderCopyBack(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    Positions[id.x] = SortTarget_Positions[id.x];
    PredictedPositions[id.x] = SortTarget_PredictedPositions[id.x];
    Velocities[id.x] = SortTarget_Velocities[id.x];
}

[numthreads(ThreadGroupSize, 1, 1)]
void ApplyAndPredict(uint3 id : SV_DispatchThreadID)
{
    Velocities[id.x] += deltaTime * float3(0, gravity, 0);  // can be external force point or something.
    PredictedPositions[id.x] = Positions[id.x] + deltaTime * Velocities[id.x];   // old
    // PredictedPositions[id.x] = Positions[id.x] + Velocities[id.x] * 1 / 120.0f;   
    
}

[numthreads(ThreadGroupSize, 1, 1)]
void CalcLagrangeOperator(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles) return;

    float3 currPos = PredictedPositions[id.x];
    float density = 0;
    
    int3 originCell = GetCell3D(currPos, smoothingRadius);
    float sqrRadius = smoothingRadius * smoothingRadius;

    float3 grad_i = float3(0.0f, 0.0f, 0.0f);
    float grad_j = 0;
    
    // Neighbour search
    for (int i = 0; i < 27; i++)
    {
        uint hash = HashCell3D(originCell + offsets3D[i]);
        uint key = KeyFromHash(hash, numParticles);
        uint currIndex = SpatialOffsets[key];       // start index?

        while (currIndex < numParticles)
        {
            uint neighbourIndex = currIndex;
            currIndex += 1;

            // looking at self? sum lambda_i here?      // need?
            // if (neighbourIndex == id.x) continue;

            uint neighbourKey = SpatialKeys[neighbourIndex];
            if (neighbourKey != key) break;

            float3 neighbourPos = PredictedPositions[neighbourIndex];
            float3 dir2Neighbour = neighbourPos - currPos;
            float sqrDstToNeighbour = dot(dir2Neighbour, dir2Neighbour);
            float dstToNeighbour = length(dir2Neighbour);

            // calc
            if (sqrDstToNeighbour > sqrRadius) continue;       // radius check, same as smoothingKernel.
            density += WPoly6(dir2Neighbour, smoothingRadius);
            float3 currGrad = WSpikyGrad(dir2Neighbour, smoothingRadius);
            currGrad = currGrad * inv_rho0;           
            grad_i += currGrad;
            if (neighbourIndex != id.x)
            {
                grad_j += dot(currGrad, currGrad);
            }
        }
    }

    Densities[id.x] = density;
    float gradSum = grad_j + dot(grad_i, grad_i);
    float constraint = max(density * inv_rho0 - 1.0f, 0.0f);
        
    float lambda = -constraint / (gradSum + lambdaEps);
    LOperator[id.x] = lambda;
}

[numthreads(ThreadGroupSize, 1, 1)]
void CalcDeltaPosition(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles) return;

    float currPos = PredictedPositions[id.x];
    int3 originCell = GetCell3D(currPos, smoothingRadius);
    float sqrRadius = smoothingRadius * smoothingRadius;
    
    float lambda_i = LOperator[id.x];
    // float WDeltaQ = WSpiky(DeltaQ, smoothingRadius);
    float WDeltaQ = WPoly6(float3(DeltaQ, 0, 0), smoothingRadius);
    float3 _deltaPos = float3(0.0f, 0.0f, 0.0f);
    
    for (int i = 0; i < 27; i++)
    {
        uint hash = HashCell3D(originCell + offsets3D[i]);
        uint key = KeyFromHash(hash, numParticles);
        uint currIndex = SpatialOffsets[key];
        while (currIndex < numParticles)
        {
            uint neighbourIndex = currIndex;
            currIndex += 1;

            uint neighbourKey = SpatialKeys[neighbourIndex];
            float lambda_j = LOperator[neighbourIndex];
            float3 neighbourPos = PredictedPositions[neighbourIndex];
            
            if (neighbourKey != key) break;

            float3 dir2Neighbour = neighbourPos - currPos;
            float sqrDstToNeighbour = dot(dir2Neighbour, dir2Neighbour);
            float dstToNeighbour = length(dir2Neighbour);

            // calc
            // float poly6 = WSpiky(dir2Neighbour, smoothingRadius);
            float poly6 = WPoly6(dir2Neighbour, smoothingRadius);
            // float S_corr = -S_corr_K * pow(abs(poly6 / WDeltaQ), S_corr_N);
            float S_corr = -S_corr_K * pow(poly6 / WDeltaQ, S_corr_N);
            float lambdaSum = lambda_i + lambda_j + S_corr;
            float3 currGrad = WSpikyGrad(dir2Neighbour, smoothingRadius);
            _deltaPos += lambdaSum * currGrad;
        }
    }
    _deltaPos = _deltaPos * inv_rho0;
    DeltaPos[id.x] = _deltaPos;
}

[numthreads(ThreadGroupSize, 1, 1)]
void UpdatePredictPosition(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles) return;
    PredictedPositions[id.x] += DeltaPos[id.x];
}

[numthreads(ThreadGroupSize, 1, 1)]
void UpdateProperty(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles) return;
    
    float3 currPos = Positions[id.x];
    float3 currPrePos = PredictedPositions[id.x];
    float3 currVel = (currPrePos - currPos) / deltaTime;
    
    
    // collision (?here)
    ResolveCollisions(currPrePos, currVel, collisionDamping);
    
    // update velocity and position
    Velocities[id.x] = currVel;
    Positions[id.x] = currPrePos;       // (?) update position here or after vv apply? 
}

[numthreads(ThreadGroupSize, 1, 1)]
void VorticityAndViscosity(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles) return;

    float3 pos = PredictedPositions[id.x];
    int3 originCell = GetCell3D(pos, smoothingRadius);
    float sqrRadius = smoothingRadius * smoothingRadius;

    float3 viscosityForce = float3(0.0f, 0.0f, 0.0f);
    float3 vorticityForce = float3(0.0f, 0.0f, 0.0f);
    float3 velocity = Velocities[id.x];

    // Neighbour search
    for (int i = 0; i < 27; i++)
    {
        uint hash = HashCell3D(originCell + offsets3D[i]);
        uint key = KeyFromHash(hash, numParticles);
        uint currIndex = SpatialOffsets[key];

        while (currIndex < numParticles)
        {
            uint neighbourIndex = currIndex;
            currIndex += 1;

            uint neighbourKey = SpatialKeys[neighbourIndex];
            // Exit if no longer looking at correct bin
            if (neighbourKey != key) break;
            
            // Skip if looking at self
            if (neighbourIndex == id.x) continue;

            float3 neighbourPos = PredictedPositions[neighbourIndex];
            float3 dir2Neighbour = neighbourPos - pos;
            float sqrDstToNeighbour = dot(dir2Neighbour, dir2Neighbour);

            // Skip if not within radius
            if (sqrDstToNeighbour > sqrRadius) continue;     // need or not? radius check can be in smoothing kernel

            float dst = sqrt(sqrDstToNeighbour);
            float3 neighbourVelocity = Velocities[neighbourIndex];
                // V_ij = V_j - V_i
            float3 V_ij = neighbourVelocity - velocity;
            
            // viscosity
            viscosityForce += V_ij * WPoly6(dir2Neighbour, smoothingRadius);
            
            // vorticity    // to be continue here
            // float3 omega = cross(V_ij, WSpikyGrad(dir2Neighbour, smoothingRadius));
            // vorticityForce = ...;
        }
    }
    // PBF: viscosityStrength set as 0.01 is good
    Velocities[id.x] += viscosityForce * viscosityStrength;     
}

// rendering //////////////////////////////////////////////////////

[numthreads(8, 8, 8)]
void UpdateDensityTexture(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= densityMapSize.x || id.y >= densityMapSize.y || id.z >= densityMapSize.z)
        return;

    // Convert threadID to a world-space position, and sample the fluid density at that point
    float3 texturePos = id / (densityMapSize - 1.0);
    float3 worldPos = (texturePos - 0.5) * boundsSize;
    DensityMap[id] = CalculateDensitiesAtPoint(worldPos)[0];
}

[numthreads(ThreadGroupSize, 1, 1)]
void UpdateWhiteParticles(uint3 id : SV_DispatchThreadID)
{
    // Early exit if thread index exceeds particle count
    if (id.x >= min(MaxWhiteParticleCount, WhiteParticleCounters[0])) return;

    // Update particle
    float deltaTime = whiteParticleDeltaTime;
    WhiteParticle particle = WhiteParticles[id.x];

    // -- Neighbour fluid partices ---
    int3 originCell = GetCell3D(particle.position, smoothingRadius);
    float sqrRadius = smoothingRadius * smoothingRadius;
    int neighbourCount = 0;
    float3 velocitySum = 0;
    float weightSum = 0;

    for (int i = 0; i < 27; i++)
    {
        uint hash = HashCell3D(originCell + offsets3D[i]);
        uint key = KeyFromHash(hash, numParticles);
        uint currIndex = SpatialOffsets[key];

        while (currIndex < numParticles)
        {
            uint neighbourIndex = currIndex;
            currIndex++;

            uint neighbourKey = SpatialKeys[neighbourIndex];
            // Exit if no longer looking at correct bin
            if (neighbourKey != key)
                break;

            float3 neighbourPos = PredictedPositions[neighbourIndex];
            float3 offsetToNeighbour = neighbourPos - particle.position;
            float sqrDstToNeighbour = dot(offsetToNeighbour, offsetToNeighbour);

            if (sqrDstToNeighbour < sqrRadius)
            {
                float dstToNeighbour = sqrt(sqrDstToNeighbour);
                float influenceWeight = SmoothingKernelPoly6(dstToNeighbour, smoothingRadius);

                // Add up velocities of nearby fluid particles, weighted smoothly by distance.
                // Also add up the weights so that we can compute the average fluid velocity.
                velocitySum += Velocities[neighbourIndex] * influenceWeight;
                weightSum += influenceWeight;

                neighbourCount += 1;
            }
        }
    }

    // -- Classify --
    bool isSpray = neighbourCount <= sprayClassifyMaxNeighbours;
    bool isBubble = neighbourCount >= bubbleClassifyMinNeighbours;
    bool isFoam = !(isSpray || isBubble);

    // Foam is transported by the fluid
    if (isFoam)
    {
        // We want the foam to be carried by the fluid (not just accelerated by it),
        // so the velocity is directly set to the average velocity of surrounding fluid.
        particle.velocity = velocitySum / weightSum;
        // Dissolve the foam over time
        particle.remainingLifetime -= deltaTime;
    }
    // Bubbles are accelerated by the surrounding fluid, and moved upwards due to buoyancy
    else if (isBubble)
    {
        const float fluidAccelMul = 3; // How fast the bubble accelerates to match velocity with fluid
        float3 accelerationBuoyancy = float3(0, gravity, 0) * (1 - bubbleBuoyancy);
        float3 accelerationFluid = (velocitySum / weightSum - particle.velocity) * fluidAccelMul;
        particle.velocity += (accelerationBuoyancy + accelerationFluid) * deltaTime;
    }
    // Spray is affected by gravity and drag
    else if (isSpray)
    {
        const float dragMultiplier = 0.04;
        float sqrSpeed = dot(particle.velocity, particle.velocity);
        float3 drag = -normalize(particle.velocity) * sqrSpeed * dragMultiplier;
        particle.velocity += (float3(0, gravity, 0) + drag) * deltaTime;
    }

    float targetScale = isBubble ? bubbleScale : 1;
    particle.scale = lerp(particle.scale, targetScale, deltaTime * bubbleScaleChangeSpeed);
    particle.position += particle.velocity * deltaTime;

    const float collisionDampening = 0.1;
    ResolveCollisions(particle.position, particle.velocity, collisionDampening);
    WhiteParticles[id.x] = particle;

    // Compact the surviving particles into temporary buffer
    if (particle.remainingLifetime > 0)
    {
        uint survivorIndex;
        InterlockedAdd(WhiteParticleCounters[1], 1, survivorIndex);
        WhiteParticlesCompacted[survivorIndex] = particle;
    }
}

[numthreads(ThreadGroupSize, 1, 1)]
void WhiteParticlePrepareNextFrame(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= MaxWhiteParticleCount) return;

    // Copy the surviving particles back into the main particle buffer
    if (id.x < WhiteParticleCounters[1])
    {
        WhiteParticles[id.x] = WhiteParticlesCompacted[id.x];
    }

    // Set active particle count to the survivor count
    if (id.x == 0)
    {
        WhiteParticleCounters[0] = WhiteParticleCounters[1];
    }
}
